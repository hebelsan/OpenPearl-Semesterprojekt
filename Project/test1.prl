
MODULE(test);
! i/o configuration of the HFU ball sorter machine

SYSTEM;
   ! test with ball sorter
!  ls1_8: PCF8574In('20'B4, 7,8) --- i2cbus;
!  ls9:   PCF8574In('21'B4, 0,1) --- i2cbus;
!  esAuf: PCF8574In('21'B4, 1,1) --- i2cbus;
!  esZu:  PCF8574In('21'B4, 2,1) --- i2cbus;
!  esNotaus:  PCF8574In('21'B4, 3,1) --- i2cbus;
!  fahr: PCF8574Out('21'B4,6,2) ---  i2cbus; ! (auf, zu)
!  hm1_7: PCF8574Out('22'B4,6,7) --- i2cbus;
!  i2cbus: I2CBus('/dev/i2c-1');
  termout: StdOut;

__cpp__(
'static pearlrt::I2CBus s_i2cbus((char*)"/dev/i2c-1");'

'pearlrt::ADS1015SE s_channel0(&s_i2cbus, 0x48,0,0);'
'pearlrt::Device*  d_channel0 = &s_channel0;'

'extern pearlrt::Device* d_channel0;'
'pearlrt::SystemDationB * _channel0 = (pearlrt::SystemDationB*) d_channel0;'

'pearlrt::ADS1015SE s_channel1(&s_i2cbus, 0x48,1,2);'
'pearlrt::Device*  d_channel1 = &s_channel1;'

'extern pearlrt::Device* d_channel1;'
'pearlrt::SystemDationB * _channel1 = (pearlrt::SystemDationB*) d_channel1;'

'static pearlrt::PCF8574Out s_hm_1_7(&s_i2cbus, 0x22, 6, 7);'
'pearlrt::Device * d_hm_1_7 = &s_hm_1_7;'
'pearlrt::SystemDationB * _hm1_7 = (pearlrt::SystemDationB*) d_hm_1_7;'

'static pearlrt::PCF8574In s_ls1_8(& s_i2cbus, 0x20, 7, 8);'
'pearlrt::Device * d_ls1_8 = &s_ls1_8;'
'pearlrt::SystemDationB * _ls1_8 = (pearlrt::SystemDationB*) d_ls1_8;'

'static pearlrt::PCF8574In s_ls9(& s_i2cbus, 0x21, 0, 1);'
'pearlrt::Device * d_ls9 = &s_ls9;'
'pearlrt::SystemDationB * _ls9 = (pearlrt::SystemDationB*) d_ls9;'

'static pearlrt::PCF8574In s_error(& s_i2cbus, 0x21, 4, 1);'
'pearlrt::Device * d_error = &s_error;'
'pearlrt::SystemDationB * _error = (pearlrt::SystemDationB*) d_error;'

'static pearlrt::PCF8574Out s_fahr(& s_i2cbus, 0x21, 6, 2);'
'pearlrt::Device * d_fahr = &s_fahr;'
'pearlrt::SystemDationB * _fahr = (pearlrt::SystemDationB*) d_fahr;'

'static pearlrt::PCF8574In s_esNotaus(& s_i2cbus, 0x21, 3, 1);'
'pearlrt::Device * d_esNotaus = &s_esNotaus;'
'pearlrt::SystemDationB * _esNotaus = (pearlrt::SystemDationB*) d_esNotaus;'

'static pearlrt::PCF8574In s_esAuf(& s_i2cbus, 0x21, 1, 1);'
'pearlrt::Device * d_esAuf = &s_esAuf;'
'pearlrt::SystemDationB * _esAuf = (pearlrt::SystemDationB*) d_esAuf;'

'static pearlrt::PCF8574In s_esZu(& s_i2cbus, 0x21, 2, 1);'
'pearlrt::Device * d_esZu = &s_esZu;'
'pearlrt::SystemDationB * _esZu = (pearlrt::SystemDationB*) d_esZu;'
);

PROBLEM;
 
 SPC termout DATION OUT SYSTEM ALPHIC GLOBAL;


DCL console DATION OUT ALPHIC DIM(*,80) FORWARD NOSTREAM CREATED(termout);
DCL uls1_8 DATION IN BASIC BIT(8) CREATED(ls1_8);
DCL uls9   DATION IN BASIC BIT(1) CREATED(ls9);
DCL uesa   DATION IN BASIC BIT(1) CREATED(esAuf);
DCL uesz   DATION IN BASIC BIT(1) CREATED(esZu);
DCL uerror   DATION IN BASIC BIT(1) CREATED(error);
DCL uesna  DATION IN BASIC BIT(1) CREATED(esNotaus);
DCL ufahr DATION OUT BASIC BIT(2) CREATED(fahr);
DCL uhm1_7 DATION OUT BASIC BIT(7) CREATED(hm1_7);

DCL ch0 DATION IN BASIC FIXED(15) CREATED(channel0);
DCL v0 FIXED(15);

DCL ch1 DATION IN BASIC FIXED(15) CREATED(channel1);
DCL v1 FIXED(15);

DCL true BIT(1) INIT('1'B1);
DCL false BIT(1) INIT('0'B1);

!semaphoren
DCL commonData SEMA PRESET(1);
DCL beginn SEMA PRESET(1);
DCL waagefrei SEMA PRESET(1);
DCL motorGo SEMA PRESET(0);
DCL mesdu SEMA PRESET(0);
DCL mesgew SEMA PRESET(0);
DCL stossen SEMA PRESET(0);
DCL motorklappe SEMA PRESET(0);
DCL sortieren1 SEMA PRESET(0);
DCL sortieren2 SEMA PRESET(0);
DCL sortieren3 SEMA PRESET(0);
DCL sortieren4 SEMA PRESET(0);
DCL sortieren5 SEMA PRESET(0);
DCL durchmesserlesen SEMA PRESET(1);


!lichtschranken
DCL ls BIT(9);
DCL (l1, l2, l3, l4, l5, l6, l7, l8, l9) BIT(1);

!motor
DCL (esa,esz,esna,err) BIT(1);
DCL driveJob BIT(2);

!klappen
DCL (current, allOff) BIT(7) INIT('0000001'B1, '0000000'B1);
DCL (k1,k2,k3,k4,k5,k6,k7) BIT(7) INIT('0000001'B1,'0000010'B1,'0000100'B1,'0001000'B1,'0010000'B1,'0100000'B1,'1000000'B1);
DCL kklein BIT(7) INIT('0001100'B1);
DCL kholz BIT(7) INIT('0010100'B1);
DCL kgummi22 BIT(7) INIT('0100100'B1);
DCL kmetall BIT(7) INIT('1000100'B1);


!kugelwerte
DCL kuduma FIXED(15) INIT(0);  ! analoger KugeldurchmesserWert
DCL realdu FLOAT INIT(0);  ! echter KugeldurchmesserWert in mm
DCL gewa FIXED(15) INIT(0);  ! analoges gewicht
DCL realgew FLOAT INIT(0);  ! echtes Kugelgewicht in g
DCL dichte FLOAT INIT(0);


dumpInputs: TASK MAIN;
   
   OPEN console;

   ACTIVATE kicker;
   ACTIVATE motor;
   ACTIVATE readInputs;
   ACTIVATE stoser;
   ACTIVATE rechner;

   PUT 'PCF8574 Test started' TO console BY A, SKIP;
   REPEAT
      PUT 'LS9-1: ', ls TO console BY A,B1(9),SKIP;
      PUT 'LS-2: ', l2, '  LS-3: ',l3, '    LS-9: ',l9 TO console BY  A,B1(1),A,B(1),A,B1(1);
      PUT ' DMP : auf: ', esa, ' zu: ', esz, ' notaus: ', esna TO console
         BY    A,        B1(1), A,    B1(1),   A, B1(1);
      PUT '  /Error: ' , err TO console BY A, B1(1);
      PUT '  durchmesr: ', realdu TO console BY A, F(6,3);
      PUT '  gew: ', realgew TO console BY A, F(6,3);
      PUT '  Dichte: ', dichte TO console BY A, F(6,3);
      PUT '  Drive: ' , driveJob TO console BY A, B1(2), SKIP; 
      AFTER 0.5 SEC RESUME;
   END;
END;


motor: TASK PRIO 3;
   DCL (auf,zu,stop) BIT(2) INIT('01'B1, '10'B1, '00'B1);
   DCL lastJob BIT(2) INIT('11'B1);
   DCL state FIXED INIT(0);

   OPEN ufahr;
   
   OPEN uesa;
   OPEN uesz;
   OPEN uesna;

   REPEAT
      REQUEST motorGo;
	   REQUEST durchmesserlesen;
	   AFTER 1 SEC RESUME; !warten bis kugel im motor ist
           state := 1; ! closing
	   SEND zu TO ufahr;   !motor schliessen

      WHILE state EQ 1 REPEAT
       TAKE esz FROM uesz;
         IF esz EQ true THEN
           SEND stop TO ufahr;
           state := 2; ! closed
         FIN;
       TAKE esna FROM uesna;
         IF esna EQ true THEN
           SEND stop TO ufahr;
           state := 3; ! notaus
         FIN;
       AFTER 0.01 SEC RESUME;
      END;

      IF state EQ 2 THEN
          TAKE v0 FROM ch0;
          kuduma := v0; ! store diameter in common data
	  RELEASE mesdu; ! jetzt kann mit durchmesser gemessen werden
          SEND auf TO ufahr;
          state := 4; ! opening
      FIN;

      IF state EQ 3 THEN
           PUT 'no ball ' TO console BY A, SKIP;
           SEND auf TO ufahr;
           state := 4; ! opening
           ! fehlerbehandklung im Ablauf fehlt
      FIN;

      WHILE state EQ 4 REPEAT
       TAKE esa FROM uesa;
         IF esa EQ true THEN
           SEND stop TO ufahr;
           state := 0;
         ELSE     
          AFTER 0.01 SEC RESUME;
         FIN;
      END;

           REQUEST waagefrei;  !klappe geht nur auf, wenn waagefrei
           SEND k2 TO uhm1_7;
           PUT 'k2 geoeffnet' TO console BY A, SKIP;
           AFTER 1 SEC RESUME;
           SEND allOff  TO uhm1_7;
           PUT 'k2 geschlossen' TO console BY A, SKIP;
           RELEASE beginn;     ! erste klappe kann geoefnnet werden
	   RELEASE motorklappe;


  END;
END;


readInputs: TASK PRIO 5;
  
   !lichtschranken
   DCL b8 BIT(8);
   DCL b1 BIT(1);
   DCL lsVorMotor BIT(1) INIT('0'B1);
   DCL (oldEsZ, oldEsA, oldESNot) BIT(1) INIT('0'B1, '1'B1, '0'B1);
   OPEN uls1_8;
   OPEN uls9;

   !motor
   OPEN uerror;

   OPEN ch0;
   OPEN ch1;

   REPEAT
      REQUEST commonData;

      TAKE b8 FROM uls1_8;
      TAKE b1 FROM uls9;
      __cpp__('_ls = _b1.bitCat(_b8);');

      !lichtschranke 1 und neun einzeln auslesen
      __cpp__(' _l9 = _ls.getBit(1);');
      __cpp__(' _l1 = _ls.getBit(9);');
      __cpp__(' _l2 = _ls.getBit(8);');
      __cpp__(' _l3 = _ls.getBit(7);');
      __cpp__(' _l4 = _ls.getBit(6);');
      __cpp__(' _l5 = _ls.getBit(5);');
      __cpp__(' _l6 = _ls.getBit(4);');     
      __cpp__(' _l7 = _ls.getBit(3);');
      __cpp__(' _l8 = _ls.getBit(2);');

      IF lsVorMotor NE l2 THEN
         lsVorMotor := l2;
         IF lsVorMotor EQ true THEN
            RELEASE motorGo;
         FIN;
      FIN;
       IF l5 EQ true THEN
         RELEASE sortieren5; ! Holzkugel zum klappen schliessen
      FIN;
      IF l6 EQ true THEN
	 RELEASE sortieren1; ! Holzkugel zum klappen schliessen
      FIN;
      IF l7 EQ true THEN
         RELEASE sortieren2; ! Gummi22 zum klappen schliessen
      FIN;
      IF l8 EQ true THEN
	 RELEASE sortieren3; ! Metall
      FIN;
      IF l9 EQ true THEN
         RELEASE sortieren4; ! Metall
      FIN;
      TAKE err FROM uerror;
      RELEASE commonData;
      AFTER 0.1 SEC RESUME;
   END;
END;



kicker: TASK PRIO 2;
  
   OPEN uhm1_7;


    REPEAT
      IF l1 EQ true THEN
       REQUEST beginn;
       SEND k1 TO uhm1_7;
       PUT 'k1 geoeffnet' TO console BY A, SKIP;
       AFTER 1 SEC RESUME;
       SEND allOff  TO uhm1_7;
       PUT 'k1 geschlossen' TO console BY A, SKIP;
       AFTER 3 SEC RESUME; !warten damit nicht gleich wieder die lichtschranke auf true geht
       !wenn nächste lichtschranke sieht und klappe 2 auf geht kann nächste kugel
      FIN;        
	
    END;
END;


stoser: TASK PRIO 4;

	REPEAT
	
         IF l3 EQ true THEN
          AFTER 4 SEC RESUME;
	  ! waage gewicht nehmen und ausrechnen
	
	  TAKE v1 FROM ch1;
          gewa := v1; ! store diameter in common data
          RELEASE mesgew; ! jetzt kann mit durchmesser gemessen werden
	  
	  REQUEST stossen; ! erst wenn dichte ausgerechnet ist darf stossen
          
	  REQUEST motorklappe; ! Wenn motorklappe wieder zu dann darf
         

	  IF realdu <= 1.5 THEN ! durchmesser in cm < 1.5 werden in klappe eins
              SEND kklein TO uhm1_7;
              RELEASE durchmesserlesen; !durchmesser kann gelesen werden
              REQUEST sortieren5;
              SEND allOff TO uhm1_7;
	      GOTO sprung;
          FIN;

	  IF dichte < 1 THEN ! Holzkugel
	    SEND kholz TO uhm1_7;
	    RELEASE durchmesserlesen; ! durchmesser von neuer kugel kann gelesen werden
	    REQUEST sortieren1;
	    SEND allOff TO uhm1_7;
	  FIN;
           
	  IF dichte < 2 THEN ! Gummi22
            IF dichte > 1 THEN
              SEND kgummi22 TO uhm1_7;
	      RELEASE durchmesserlesen; !durchmesser kann gelesen werden
              REQUEST sortieren2;
              SEND allOff TO uhm1_7;
	    FIN;
          FIN;

	  IF dichte < 4 THEN ! metall
	    IF dichte > 2 THEN
	      SEND kmetall TO uhm1_7;
	      RELEASE durchmesserlesen; ! durchmesser
	      REQUEST sortieren3;
	      SEND allOff TO uhm1_7;
	    FIN;
     	   FIN;

	   IF dichte >= 4 THEN ! gummigrau
	      SEND k3 TO uhm1_7;
	      RELEASE durchmesserlesen; !durchmesser 
              REQUEST sortieren4;
              SEND allOff TO uhm1_7;
           FIN;

   sprung: RELEASE waagefrei;	    
	    

         FIN;

        END;
END;


rechner: TASK PRIO 10;

! wie macht man konstante ?

DCL faktorD FLOAT INIT(0.00124016);	
DCL faktorG FLOAT INIT(0.002360356);
DCL pi FLOAT INIT(3.14159265); ! 3.14159265359 
DCL volumen FLOAT INIT(0);
DCL duhoch3 FLOAT; ! Durchmesser hoch 3
DCL zwischen FLOAT INIT(0.5235987756);

! Faktor zum umrechnen in reale Durchmesser in mm = 0,00120953 oder 0,00124016
! Faktor zum umrechnen in reale Gewicht in g = 0,002360356
! Volumen von kugel = 1/6*PI*d³
! 1/6*PI = 0.5235987756
! Dichte = m/V

	REPEAT
	  REQUEST mesdu; ! messen von Durchmesser
	  realdu := kuduma * faktorD;  ! umrechnen in realen durchmesser
          realdu := realdu/10; ! umrechnen von mm in cm
 
	  REQUEST mesgew;
          realgew := gewa * faktorG;  ! umrechnen in reales Gewicht
          
	  duhoch3 := realdu*realdu*realdu; 
	  volumen := zwischen * duhoch3;	  
  	  dichte := realgew/volumen;	  
	  RELEASE stossen;
	END;
END;

MODEND;
